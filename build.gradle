buildscript {
    ext.kotlin_version = '1.3.41'
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.41'
}
apply from: 'dependencies.gradle'
group 'com.revolut'
version '1.0-SNAPSHOT'

dependencies {
    testImplementation project(":debit-service")
    testImplementation project(":credit-service")
    testImplementation project(":balance-service")
    testImplementation project(":transaction-service")
}

allprojects {
    repositories {
        mavenCentral()
        jcenter()
    }
    apply plugin: 'org.jetbrains.kotlin.jvm'
    apply plugin: 'kotlin'

    compileKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }
    compileTestKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
        implementation libraries.LOGGER
        compile libraries.COROUTINE

        if (project.name != "domain-model") {
            implementation libraries.SERIALIZER
            testCompile libraries.TEST
            testRuntimeOnly libraries.TEST_RUNTIME
            if (project.name != "ORMK") {
                implementation project(':domain-model')
            }
        }
    }
    test {
        useJUnitPlatform()
        forkEvery = 1
        failFast = false
    }
    clean {
        delete 'activemq-data'
    }
}




project(":transaction-service") {
    dependencies {
        implementation project(":ORMK")
        implementation project(":message-broker-service")

        implementation libraries.RESTSERVER
        implementation libraries.RESTCLIENT

        testImplementation project(":debit-service")
        testImplementation project(":credit-service")
        testImplementation project(":balance-service")

    }
}

project(":domain-model") {
    dependencies {
        implementation project(":ORMK")
        implementation libraries.RESTSERVER

    }
}

project(":credit-service") {
    dependencies {
        implementation project(":ORMK")
        implementation project(":message-broker-service")
        implementation project(":balance-service")

    }
}

project(":debit-service") {
    dependencies {
        implementation project(":ORMK")
        implementation project(":message-broker-service")
        implementation project(":lockmanager-service")
        implementation project(":balance-service")
    }
}

project(":balance-service") {
    dependencies {
        implementation project(":ORMK")
        implementation project(":message-broker-service")
        implementation project(":lockmanager-service")

    }
}

project(":lockmanager-service") {
    dependencies {
        implementation project(":ORMK")
        implementation project(":message-broker-service")
    }
}


project(":ORMK") {
    dependencies {
        implementation libraries.H2
        compile libraries.OBJINT
    }
}

project(":message-broker-service") {
    dependencies {
        implementation libraries.AMQ
        compile libraries.OBJINT
    }
}

